group("all") {
  deps = [
    ":version-consistency.check",
    "//test",
  ]
}

source_set("include") {
  sources = [
    "jasl_inner/jasl_common.hpp",
    "jasl_inner/jasl_diagnostic.hpp",
    "jasl_inner/jasl_feature_test_macro.hpp",
    "jasl_inner/jasl_murmurhash3.hpp",
    "jasl_inner/jasl_string_view_bridge.hpp",
    "jasl_static_string.hpp",
    "jasl_string.hpp",
    "jasl_string_view.hpp",
  ]

  public_configs = [ ":public_config" ]
}

config("public_config") {
  include_dirs = [ "//include" ]
}

action("version-consistency.check") {
  script = "//build/check.py"

  args = [
    "version-consistency",
    "--changelog",
    rebase_path("//CHANGELOG.md"),
    "--jasl_common",
    rebase_path("//include/jasl_inner/jasl_common.hpp"),
    "--doxygen_config",
    rebase_path("//doxygen.cfg"),
    "--output",
    target_name,
  ]

  deps = [
    ":include",
  ]
  sources = [
    "//CHANGELOG.md",
    "//doxygen.cfg",
  ]
  outputs = [
    "$root_out_dir/$target_name",
  ]
}
